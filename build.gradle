plugins {
	id 'java'
}

repositories {
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'
def pluginMainClass = 'com.livepid.LivePidPluginTest'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'com.livepid'
version = '1.0-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('run', JavaExec) {
	classpath = sourceSets.test.runtimeClasspath
	mainClass = pluginMainClass

	jvmArgs "-ea"
	def runeliteDevHome = System.getenv("RUNELITE_DEV_HOME")
	if (runeliteDevHome != null && !runeliteDevHome.trim().isEmpty()) {
		jvmArgs "-Duser.home=${runeliteDevHome}"
	}
	def runeliteLauncherVersion = System.getenv("RUNELITE_LAUNCHER_VERSION")
	if (runeliteLauncherVersion != null && !runeliteLauncherVersion.trim().isEmpty()) {
		jvmArgs "-Drunelite.launcher.version=${runeliteLauncherVersion.trim()}"
	}
	args "--developer-mode", "--debug"
	def runeliteSafeMode = System.getenv("RUNELITE_SAFE_MODE")
	if (runeliteSafeMode == null || !(runeliteSafeMode.trim().equalsIgnoreCase("0") || runeliteSafeMode.trim().equalsIgnoreCase("false"))) {
		args "--safe-mode"
	}
	def runeliteExtraArgs = System.getenv("RUNELITE_EXTRA_ARGS")
	if (runeliteExtraArgs != null && !runeliteExtraArgs.trim().isEmpty()) {
		args runeliteExtraArgs.trim().split("\\s+")
	}
}

tasks.named('jar', Jar).configure {
	from('runelite-plugin.properties')
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	manifest {
		attributes('Multi-Release': true)
	}
}
